module engine

import io
import io/time

import game_engine_lib/math
import game_engine_lib/ecs
import game_engine_lib/input

record Position(value: Vector2)
record Rotation(value: Double)
record Scale(value: Vector2)
record Time(deltaTime: Double, gameTime: Double, frame: Int)

def default(): Position = Position(zero())
def default(): Rotation = Rotation(0.0)
def default(): Scale = Scale(one())

def limitRotation(rotation: Double): Double = {
  val twoPi = 2.0 * PI;
  val rotations = floor(rotation / twoPi);
  val limited = rotation - rotations.toDouble() * twoPi;
  if (limited > PI) {
    limited - twoPi
  } else {
    limited
  }
}

def uniformScale(scale: Double): Vector2 = {
  Vector2(scale, scale)
}

def engineWorld() { prog: => Unit / {
  Resource[Time],
  Component[Position], Component[Rotation], Component[Scale],
  Component[Option[Position]], Component[Option[Rotation]], Component[Option[Scale]],
  Components[Position], Components[Rotation], Components[Scale],
  Components[Option[Position]], Components[Option[Rotation]], Components[Option[Scale]],
  Components[Unit], 
  nextComponentId, nextEntityId, ArchManager, EntityManager, System, World
} }: Unit = {
  jsInitInput();
  with world();
  with component[Position]();
  with component[Rotation]();
  with component[Scale]();
  with defaultEngineSystem();
  prog();
}

def defaultEngineSystem() { prog: => Unit / {
  Resource[Time],
  System
} }: Unit / {
  Resource[RunWorld],
  Components[Unit],
  nextEntityId, System, ArchManager, EntityManager, World
} = {
  with createResource[Time](updateTime(Time(0.0, 0.0, -1)));
  with addSystem[Unit, Unit, Unit]() { { _ } =>
    do setResource[Time](updateTime(do getResource[Time]()));
    jsClearFrameInput();
    if (jsGetKey("Escape")) {
      do setResource[RunWorld](RunWorld(false));
    }
  };
  prog();
}

def updateTime(time: Time): Time = {
  val now = jsGetTime();
  Time(now - time.gameTime, now, time.frame + 1)
}

extern pure def jsGetTime(): Double =
  js "window.performance.now() / 1000.0"