module renderer

import heap

import game_engine_lib/math
import game_engine_lib/core
import game_engine_lib/engine
import game_engine_lib/ecs

extern type CanvasRenderingContext2D

type Shape {
  Circle(color: Color);
  Rect(color: Color);
  Text(color: Color, text: String);
}
record DrawHeight(value: Double)
record Camera(position: Vector2, rotation: Double, height: Double)
record WindowProperties(size: Vector2Int)

record RenderObject(
  entityId: Int,
  position: Vector2,
  rotation: Double,
  scale: Vector2,
  shape: Shape,
  drawHeight: Double
)

def default(): DrawHeight = DrawHeight(0.0)

def compareRenderObjectHeight(n: RenderObject, m: RenderObject): Ordering = {
  if (n.drawHeight == m.drawHeight) {
    compareInt(n.entityId, m.entityId)
  } else if (n.drawHeight < m.drawHeight) {
    Less()
  } else {
    Greater()
  }
}

def canvasRenderer() { prog: => Unit / {
  Resource[Camera], Resource[WindowProperties],
  Component[Shape], Component[DrawHeight],
  System
} }: Unit / {
  Component[Position], Component[Rotation], Component[Scale],
  System, EntityIdManager,
  ComponentManager, ArchManager, EntityManager, World
} = {
  val context = jsGetContext();
  jsInitCanvas();
  jsInitFont(context);
  with component[Shape]();
  with component[DrawHeight]();
  with createResource[WindowProperties](getWindowProperties());
  with createResource[Camera](Camera(
    zero(),
    0.0,
    10.0
  ));
  // Predict render object count
  var lastRenderObjectCount = 0;
  with def query = query();
  with def positions = query.addC[Position]();
  with def optRotations = query.addOptC[Rotation]();
  with def optScales = query.addOptC[Scale]();
  with def shapes = query.addC[Shape]();
  with def optDrawHeights = query.addOptC[DrawHeight]();
  with system() {
    do setResource[WindowProperties](getWindowProperties());
    jsResetTransform(context);
    jsClearCanvas(context);
    jsTranslate(context, jsGetCanvasWidth().toDouble() / 2.0, jsGetCanvasHeight().toDouble() / 2.0);
    setCameraTransform(context, do getResource[Camera]());
    val renderHeap = heap(compareRenderObjectHeight, lastRenderObjectCount + 4);
    lastRenderObjectCount = 0;
    query.foreach() { (entity) =>
      lastRenderObjectCount = lastRenderObjectCount + 1;
      renderHeap.insert(
        RenderObject(
          entity.id,
          positions.get().value,
          (optRotations.get().getOrElse() { default() }).value,
          (optScales.get().getOrElse() { default() }).value,
          shapes.get(),
          (optDrawHeights.get().getOrElse() { default() }).value
        )
      );
    }
    while (renderHeap.size() > 0) {
      with panicOn[OutOfBounds, Unit]();
      val renderObject = renderHeap.deleteMin();
      jsSave(context);
      setTransform(context, renderObject.position, renderObject.rotation, renderObject.scale);
      drawShape(context, renderObject.shape);
      jsRestore(context);
      ()
    }
  };
  prog();
}

def getWindowProperties(): WindowProperties = {
  WindowProperties(Vector2Int(jsGetCanvasWidth(), jsGetCanvasHeight()))
}

def setTransform(context: CanvasRenderingContext2D, position: Vector2, rotation: Double, scale: Vector2): Unit = {
  jsTranslate(context, position.x, 0.0 - position.y);
  jsRotate(context, rotation);
  jsScale(context, scale.x, scale.y);
}

def setCameraTransform(context: CanvasRenderingContext2D, camera: Camera): Unit = {
  jsRotate(context, 0.0 - camera.rotation);
  val screenScale = jsGetCanvasHeight().toDouble();
  jsScale(context, screenScale / camera.height, screenScale / camera.height);
  jsTranslate(context, 0.0 - camera.position.x, camera.position.y);
}

def drawShape(context: CanvasRenderingContext2D, shape: Shape): Unit = {
  shape match {
    case Circle(color) =>
      jsFillStyle(context, color.show());
      jsFillCircle(context);
    case Rect(color) =>
      jsFillStyle(context, color.show());
      jsFillRect(context);
    case Text(color, text) =>
      jsFillStyle(context, color.show());
      jsFillText(context, text);
  }
}

extern io def jsInitCanvas(): Unit =
  js "initCanvas()"

extern pure def jsGetCanvasWidth(): Int =
  js "canvas.width"

extern pure def jsGetCanvasHeight(): Int =
  js "canvas.height"

extern pure def jsGetContext(): CanvasRenderingContext2D =
  js "canvas.getContext('2d')"

extern io def jsClearCanvas(context: CanvasRenderingContext2D): Unit =
  js "clearCanvas(${context})"

extern io def jsFillStyle(context: CanvasRenderingContext2D, color: String): Unit =
  js "(${context}).fillStyle = ${color}"

extern io def jsFillRect(context: CanvasRenderingContext2D): Unit =
  js "(${context}).fillRect(-0.5, -0.5, 1, 1)"

extern io def jsFillCircle(context: CanvasRenderingContext2D): Unit =
  js "fillCircle(${context})"

extern io def jsInitFont(context: CanvasRenderingContext2D): Unit =
  js "initFont(${context}, 1)"

extern io def jsFillText(context: CanvasRenderingContext2D, text: String): Unit =
  js "(${context}).fillText(${text}, 0, 0.36)"

extern io def jsTranslate(context: CanvasRenderingContext2D, x: Double, y: Double): Unit =
  js "(${context}).translate(${x}, ${y})"

extern io def jsRotate(context: CanvasRenderingContext2D, rotation: Double): Unit =
  js "(${context}).rotate(${rotation})"

extern io def jsScale(context: CanvasRenderingContext2D, x: Double, y: Double): Unit =
  js "(${context}).scale(${x}, ${y})"

extern io def jsResetTransform(context: CanvasRenderingContext2D): Unit =
  js "(${context}).resetTransform()"

extern io def jsSave(context: CanvasRenderingContext2D): Unit =
  js "(${context}).save()"

extern io def jsRestore(context: CanvasRenderingContext2D): Unit =
  js "(${context}).restore()"

extern js """

let canvas = null;

function initCanvas() {
  document.head.innerHTML += `
    <style>
    html, body, canvas { padding: 0; margin: 0; }
    canvas { display:block; }
    </style>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Effekt Game Engine</title>
  `;
  document.body.innerHTML += `
    <canvas id="myCanvas" width="640" height="360"></canvas>
  `;
  canvas = document.getElementById('myCanvas');
  window.addEventListener('resize', resizeCanvas);
  resizeCanvas();
}

function initFont(context, fontSize) {
  context.font = 'bold ' + fontSize + 'px sans';
  context.textAlign = 'center';
}

function fillCircle(context) {
  context.beginPath();
  context.arc(0, 0, 0.5, 0, 2 * Math.PI);
  context.fill();
}

function resizeCanvas() {
  canvas.width = window.innerWidth;
  canvas.height = window.innerHeight;
}

function clearCanvas(context) {
  context.fillStyle = "rgb(0 0 0)";
  context.fillRect(0, 0, canvas.width, canvas.height);
}

"""
